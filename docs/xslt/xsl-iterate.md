---
description: Используется для итерации последовательности, с возможностью установки параметров для использования в следующей итерации
---

# xsl:iterate

Используется для итерации последовательности с возможностью задания параметров для использования в следующей итерации.

_Доступен в XSLT 3.0. Начиная с Saxon 9.8, доступен во всех редакциях. Реализован в Saxon-PE и Saxon-EE начиная с Saxon 9.6._

-   **Категория**: instruction
-   **Содержимое**: ( [`xsl:param`](xsl-param.md)\* , [`xsl:on-completion`](xsl-on-completion.md)? , _sequence-constructor_ )
-   **Допустимые родительские элементы**: any XSLT element whose content model is _sequence-constructor_; any literal result element

## Атрибуты

**`select`**
: _expression_
: Входная последовательность, над которой обрабатывается итерация.

## Заметки по реализации Saxon

Порядок элементов в `xsl:iterate` изменился со времен первоначального рабочего проекта W3C ([`xsl:on-completion`](xsl-on-completion.md) теперь идет сразу после [`xsl:param`](xsl-param.md)). Начиная с версии Saxon 9.7, разрешено только новое упорядочивание. В Saxon 9.6 допускается как новое, так и старое упорядочивание, но при использовании старого упорядочивания выдается предупреждение.

В более ранних версиях Saxon прототип `xsl:iterate` был реализован как расширение в пространстве имен Saxon (`saxon:iterate`). В Saxon 9.5 это расширение было упразднено.

Чтобы `xsl:iterate` был потоковым, правила W3C требуют, чтобы выражение `select` было "striding", что означает, что оно может использовать дочернюю ось, но не ось потомков (чтобы выбранные узлы не перекрывали друг друга). До версии Saxon 9.5 Saxon пытался быть более либеральным и разрешить ограниченный поток также при использовании оси потомка. Начиная с версии Saxon 9.6, Saxon был приведен в соответствие со спецификацией W3C. Во многих случаях ограничение можно обойти, используя функцию outermost, например, выражение `outermost(//title)` является striding, несмотря на то, что в нем используется ось descendant.

## Подробности

Инструкция `xsl:iterate` является новой в XSLT 3.0. Она похожа на [`xsl:for-each`](xsl-for-each.md), за исключением того, что элементы входной последовательности обрабатываются последовательно, и после обработки каждого элемента входной последовательности можно задать параметры для использования в следующей итерации. Поэтому его можно использовать для решения проблем, для которых в XSLT 2.0 требовались рекурсивные функции или шаблоны.

Инструкция `xsl:iterate` мотивирована случаями использования для потоковой обработки, но она также может быть выгодно использована в ситуациях, не связанных с потоковой обработкой. Для получения дополнительной информации смотрите Потоковая передача с `xsl:iterate`.

Инструкция допускает дочерний элемент [`xsl:on-completion`](xsl-on-completion.md), который определяет обработку, выполняемую при исчерпании входной последовательности. Инструкции внутри `xsl:on-completion` имеют доступ к конечным значениям параметров, объявленных в наборе инструкций [`xsl:next-iteration`](xsl-next-iteration.md) при обработке последнего элемента в последовательности. Элемент [`xsl:break`](xsl-break.md) может быть использован внутри вложенного конструктора последовательности инструкции `xsl:iterate`, что вызывает преждевременное завершение до обработки всей входной последовательности.

## Примеры

### Пример 1

Вычисляет текущий баланс последовательности финансовых операций:

```xslt
<xsl:iterate select="transactions/transaction">
    <xsl:param name="balance" select="0.00" as="xs:decimal"/>
    <xsl:variable name="newBalance"
        select="$balance + xs:decimal(@value)"/>
    <balance date="{@date}" value="{$newBalance}"/>
    <xsl:next-iteration>
        <xsl:with-param name="balance" select="$newBalance"/>
    </xsl:next-iteration>
</xsl:iterate>
```

### Пример 2

Копирует входную последовательность до первого элемента `<br>`:

```xslt
<xsl:iterate select="*">
    <xsl:choose>
        <xsl:when test="self::br">
            <xsl:break/>
        </xsl:when>
        <xsl:otherwise>
            <xsl:copy-of select="."/>
        </xsl:otherwise>
    </xsl:choose>
</xsl:iterate>
```

## Ссылки

-   [XSLT 3.0 Specification](http://www.w3.org/TR/xslt-30/#element-iterate)
-   [Saxon](https://www.saxonica.com/html/documentation/xsl-elements/iterate.html)

## См. также

-   [`xsl:next-iteration`](xsl-next-iteration.md)
-   [`xsl:break`](xsl-break.md)
-   [`xsl:on-completion`](xsl-on-completion.md)
-   [`xsl:for-each`](xsl-for-each.md)
